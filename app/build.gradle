apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

androidExtensions {
    experimental = true
    defaultCacheImplementation = "SPARSE_ARRAY"
}

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {

    dataBinding {
        enabled = true
    }

    compileSdkVersion 29
    buildToolsVersion "29.0.1"
    defaultConfig {
        applicationId "com.nesst"
        minSdkVersion 17
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary true
        multiDexEnabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    applicationVariants.all { variant ->
        /**
         * build apk name
         */
        variant.outputs.all { output ->
            def project = "Nesst"
            def SEP = "_"
//            def flavor = variant.productFlavors[0].name
            def version = variant.versionName
            def date = new Date()
            def formattedDate = date.format('ddMMyy_HHmm')
            def newApkName = project + SEP + version + SEP + formattedDate + ".apk"
            outputFileName = new File(newApkName)
        }
    }

    /**
     * resolve and fix conflicting dependencies
     */
    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
            preferProjectModules()
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix '.' + getVersionName()
            /*buildConfigField("String[]", "builds", getBuilds())*/
        }
    }

    packagingOptions {
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/notice'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license'
        exclude 'META-INF/license.txt'
    }

    dynamicFeatures = [":requests", ":wallet"]
}

dependencies {

    api 'com.google.android.material:material:1.0.0'
    api 'androidx.vectordrawable:vectordrawable:1.1.0'
    api 'net.steamcrafted:materialiconlib:1.1.5'
    api 'androidx.navigation:navigation-fragment-ktx:2.2.0-alpha02'
    api 'androidx.navigation:navigation-ui-ktx:2.2.0-alpha02'
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'io.reactivex.rxjava2:rxandroid:2.1.1'
    api 'com.tapadoo.android:alerter:4.0.2'
    api 'com.github.florent37:shapeofview:v1.0.7'
    api 'com.github.ybq:Android-SpinKit:1.2.0'
    api 'com.github.dev4vin:android-promise-location:1.0.2'
    api 'org.apache.commons:commons-lang3:3.9'
    api 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'
//    api 'com.github.android-promise:promise-ui:1.0-beta0'
    api 'joda-time:joda-time:2.10.3'
    api project(path: ':nesstbase')
    implementation project(':flutter')

    // google deps
    implementation 'com.google.firebase:firebase-analytics:17.2.0'
    //
    implementation 'com.squareup.picasso:picasso:2.71828'
    runtimeOnly 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    runtimeOnly 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.3@aar'
    runtimeOnly "androidx.preference:preference-ktx:1.1.0"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.nineoldandroids:library:2.4.0'
    kapt 'com.google.dagger:dagger-compiler:2.24'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
